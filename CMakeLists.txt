cmake_minimum_required(VERSION 3.18...3.20)
cmake_policy(VERSION 3.18)

cmake_policy(SET CMP0069 NEW)
set(CMAKE_POLICY_DEFAULT_CMP0069 NEW)

project(LuisaCompute LANGUAGES C CXX VERSION 0.1)

if (NOT DEFINED LUISA_COMPUTE_MASTER_PROJECT)
    if (CMAKE_CURRENT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
        set(LUISA_COMPUTE_MASTER_PROJECT ON)
    else ()
        set(LUISA_COMPUTE_MASTER_PROJECT OFF)
    endif ()
endif ()

set(CMAKE_C_STANDARD 11)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_OSX_DEPLOYMENT_TARGET 12)
set(BUILD_SHARED_LIBS ON)

if (NOT MSVC OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    find_program(SCCACHE_EXE sccache)
    if (SCCACHE_EXE)
        message(STATUS "Build with sccache: ${SCCACHE_EXE}")
        set(CMAKE_C_COMPILER_LAUNCHER ${SCCACHE_EXE})
        set(CMAKE_CXX_COMPILER_LAUNCHER ${SCCACHE_EXE})
        set(CMAKE_OBJC_COMPILER_LAUNCHER ${SCCACHE_EXE})
        set(CMAKE_OBJCXX_COMPILER_LAUNCHER ${SCCACHE_EXE})
        set(CMAKE_CUDA_COMPILER_LAUNCHER ${SCCACHE_EXE})
    else ()
        message(STATUS "Could not find sccache")
    endif ()
    include(CheckIPOSupported)
    check_ipo_supported(RESULT IPO_SUPPORTED OUTPUT CHECK_IPO_SUPPORTED_OUTPUT LANGUAGES C CXX)
    if (IPO_SUPPORTED)
        message(STATUS "Build with IPO/LTO")
        set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ON)
    else ()
        message(STATUS "Build without IPO/LTO")
    endif ()
endif ()

option(LUISA_COMPUTE_BUILD_TESTS "Build tests for LuisaCompute" ${LUISA_COMPUTE_MASTER_PROJECT})

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin")
foreach (CONFIG ${CMAKE_CONFIGURATION_TYPES})
    string(TOUPPER ${CONFIG} CONFIG_UPPER)
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${CONFIG_UPPER} "${CMAKE_BINARY_DIR}/${CONFIG}/bin")
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${CONFIG_UPPER} "${CMAKE_BINARY_DIR}/${CONFIG}/bin")
endforeach ()

set(CMAKE_FIND_PACKAGE_SORT_ORDER NATURAL)
set(CMAKE_FIND_PACKAGE_SORT_DIRECTION DEC)

if (MSVC)
    string(REGEX REPLACE "/EH[a-z]+" "" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /EHsc")
endif ()

add_subdirectory(src)
